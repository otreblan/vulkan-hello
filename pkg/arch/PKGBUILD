# Maintainer: Otreblan <otreblain@gmail.com>

pkgname=vulkan-hello-git
pkgver=1.0.0.r13.gdffe7b5
pkgrel=1
epoch=
pkgdesc="Vulkan"
arch=('x86_64')
url="https://github.com/otreblan/hello"
license=('GPL3')
groups=()
depends=(
	'assimp'
	'boost-libs'
	'bullet'
	'glfw'
	'imgui'
	'vulkan-driver'
	'vulkan-icd-loader'
)
makedepends=(
	'boost'
	'cmake'
	'cpp-taskflow'
	'entt'
	'git'
	'glm'
	'shaderc'
	'stb'
	'vulkan-headers'
	'vulkan-memory-allocator'
	'xdg-utils-cxx'
)
checkdepends=()
optdepends=()
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
_gitrepo=$(git rev-parse --show-toplevel)
_headcommit=$(git rev-parse HEAD)
source=("$pkgname::git+file://$_gitrepo#commit=$_headcommit")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	( set -o pipefail
	git describe --long 2>/dev/null | sed 's/^v-//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cmake -B build -S "$pkgname" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
		-DCMAKE_UNITY_BUILD=ON

	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build
}

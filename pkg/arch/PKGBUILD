# Maintainer: Otreblan <otreblain@gmail.com>

pkgname=vulkan-hello-git
pkgver=0.0.0.r48.g42df7e0
pkgrel=1
epoch=
pkgdesc="Vulkan"
arch=('x86_64')
url="https://github.com/otreblan/hello"
license=('GPL3')
groups=()
depends=('vulkan-icd-loader' 'glfw')
makedepends=(
	'cmake'
	'git'
	'glm'
	'shaderc'
	'stb'
	'vulkan-headers'
	'xdg-utils-cxx'
)
checkdepends=()
optdepends=()
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
_gitrepo=$(git rev-parse --show-toplevel)
_headcommit=$(git rev-parse HEAD)
source=("$pkgname::git+file://$_gitrepo#commit=$_headcommit" profile.tar.gz)
md5sums=('SKIP' 'SKIP')

prepare() {
	mkdir -p "$srcdir/$pkgname/build"
}

pkgver() {
	cd "$srcdir/$pkgname"
	( set -o pipefail
	git describe --long 2>/dev/null | sed 's/^v-//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cd "$srcdir/$pkgname/build"

	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_UNITY_BUILD=ON \
		-DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
		-DCMAKE_EXPORT_COMPILE_COMMANDS=ON \
		-DCMAKE_CXX_FLAGS="$CXXFLAGS -fprofile-use=\"$srcdir/profile\" -fprofile-correction" \
		..

		#-DCMAKE_CXX_FLAGS="$CXXFLAGS -fprofile-generate=profile" \
	make
}

package() {
	cd "$srcdir/$pkgname/build"
	make DESTDIR="$pkgdir/" install
}
